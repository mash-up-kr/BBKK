buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'org.seoul'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'test'
}

def queryDslOutput = file("src-gen/main/java")

sourceSets {
    main {
        java {
            srcDir "src-gen/main/java"
        }
    }
}

clean {
    delete queryDslOutput
}

task generateQueryDSL(type: JavaCompile, group: "build") {
    doFirst {
        if (!queryDslOutput.exists()) {
            logger.info("Creating `$queryDslOutput` directory")

            if (!queryDslOutput.mkdirs()) {
                throw new InvalidUserDataException("Unable to create `$queryDslOutput` directory")
            }
        }
    }

    source = sourceSets.main.java
    classpath = configurations.compile
    options.compilerArgs = [
            "-proc:only",
            "-processor",
            "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = queryDslOutput
    options.failOnError = false
}

compileJava {
    dependsOn generateQueryDSL //이 부분 dependsOn이 generateQueryDSL이다.
    source generateQueryDSL.destinationDir
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("com.querydsl:querydsl-core:4.2.1")
    compile("com.querydsl:querydsl-apt:4.2.1")
    compile("com.querydsl:querydsl-jpa:4.2.1")
    compile("com.querydsl:querydsl-collections:4.2.1")

    compile("org.springframework.boot:spring-boot-starter-jdbc")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-validation")
    compile("org.apache.commons:commons-dbcp2:2.1.1")
    compile("commons-io:commons-io:2.4")
    compile("commons-codec:commons-codec:1.10")
    compile("com.google.guava:guava:25.1-jre")
    compile("com.amazonaws:aws-java-sdk:1.11.399")

    runtime("com.h2database:h2")
    runtime("mysql:mysql-connector-java")

    compileOnly("org.projectlombok:lombok")
    compileOnly("org.springframework.boot:spring-boot-configuration-processor")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.junit.platform:junit-platform-launcher:1.1.1")
    testCompile("org.junit.platform:junit-platform-suite-api:1.1.1")
    testCompile("org.junit.jupiter:junit-jupiter-engine:5.1.1")
    testCompile("org.junit.vintage:junit-vintage-engine:5.1.1")
    testCompile("org.mockito:mockito-core:2.18.0")
}